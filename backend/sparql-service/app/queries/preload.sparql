PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX wikibase: <http://wikiba.se/ontology#>
PREFIX bd: <http://www.bigdata.com/rdf#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

SELECT DISTINCT ?band ?bandLabel ?genreLabel ?countryLabel ?startYear ?influencerLabel WHERE {
  # 1. Filtru Popularitate (peste 5 link-uri)
  ?band wdt:P31 wd:Q215380; 
        wikibase:sitelinks ?sitelinks.
  FILTER(?sitelinks > 5) 

  # 2. Date de bază
  ?band wdt:P136 ?genre;
        wdt:P495 ?country;
        wdt:P571 ?date.

  BIND(YEAR(?date) AS ?startYear).

  # 3. RELAȚIA DE INFLUENȚĂ (Use-Case Complex)
  OPTIONAL {
    ?band wdt:P737 ?influencer.
    ?influencer rdfs:label ?influencerLabel.
    FILTER(LANG(?influencerLabel) = "en")
  }

  SERVICE wikibase:label { bd:serviceParam wikibase:language "en". }
} 
LIMIT 3500